plugins {
    id "java-library"
    id "maven-publish"
    id "com.gradleup.shadow" version "9.0.0-beta6"
}

group = "dev.digitality"
version = "1.1.0"
description = "DigitalGUI"

java.sourceCompatibility = JavaLanguageVersion.of(8)
java.targetCompatibility = JavaLanguageVersion.of(8)
compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()

    maven {
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }

    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    maven {
        url = "https://repo.codemc.org/repository/maven-public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT"
    implementation "de.tr7zw:item-nbt-api:2.14.1"

    compileOnly "org.jetbrains:annotations:26.0.2"
    compileOnly "org.projectlombok:lombok:1.18.36"
    annotationProcessor "org.projectlombok:lombok:1.18.36"
}

shadowJar {
    minimize()

    relocate "de.tr7zw.changeme.nbtapi", "dev.digitality.digitalgui.nbtapi"

    archiveClassifier = null
}

tasks.register("sourcesJar", Jar) {
    archiveClassifier = "sources"
    from(sourceSets.main.allSource)
}

publishing {
    repositories {
        maven {
            name "goldzoneRepoPrivate"
            url "https://repo.gold-zone.cz/releases"

            credentials {
                username project.findProperty("goldzoneRepo.username") ?: System.getenv("GOLDZONE_REPO_USERNAME")
                password project.findProperty("goldzoneRepo.password") ?: System.getenv("GOLDZONE_REPO_PASSWORD")
            }

            authentication {
                basic(BasicAuthentication)
            }
        }
    }

    publications {
        maven(MavenPublication) { publication ->
            groupId = this.group
            artifactId = this.name.toLowerCase()
            version = this.version

            from(components.shadow)
            artifact(tasks.sourcesJar)
        }
    }
}